import level0

//level column that the spawn triggers on, MUST BE SORTED LOW TO HIGH
array(word) spawn_column_locations [32] align(64)
//y-location (pixel row) the new object is spawned at, and it's type
//hi-byte is the type, lo-byte is the y-coordinate
array(word) spawn_info [32] align(64)
//the indices for this array correspond to the indices for each
//spawn location in spawn_column_locations
array spawner_flags [32] align(32)
//maximum spawn locations for this level
byte num_spawn_locations
//x-coordinate for enemies spawned while scrolling
const byte spawn_x_loc_right = $F0
const byte spawn_x_loc_left = $10

void load_level(byte level_num) {
    byte i
    word leftmost_column
    word rightmost_column
    word colnum
    word xloc
    
    if level_num == 0 {
        load_level0()
    }
    
    touched_goal = 0
    win_timer = 0
    current_level = level_num
    
    //load any enemies that spawn on the first screen
    leftmost_column = current_screen 
    leftmost_column <<= 5
    rightmost_column = leftmost_column
    rightmost_column += 32
    for i,0,until,num_spawn_locations {
        colnum = spawn_column_locations[i]
        if colnum >= leftmost_column && colnum < rightmost_column {
            xloc = colnum
            xloc -= leftmost_column
            xloc <<= 3
            spawn_enemy(spawn_info[i].hi,xloc.lo,spawn_info[i].lo, i)
        }
    }
}

void load_screen(byte screen_num) {
    current_screen = screen_num
    draw_screen_by_pointers(screen_num)
}

void draw_screen_by_pointers(byte screen_num) {
    byte i
    byte j
    
    right_draw_tiles = current_level_tiles
    left_draw_tiles = current_level_tiles
    right_draw_attrs = current_level_attrs
    left_draw_attrs = current_level_attrs
    
    //now set the pointers to the appropriate screen
    for i,0,until,screen_num {
        increment_draw_ptr_by_screen(right_draw_tiles)
        increment_draw_ptr_by_screen(left_draw_tiles)
        increment_attr_ptr_by_screen(right_draw_attrs)
        increment_attr_ptr_by_screen(left_draw_attrs)
    }
    //fill the collision map
    for i,0,until,$10 {
        fill_collis_column_from_ptr(i,right_draw_tiles)
        increment_draw_ptr(right_draw_tiles)
    }
    //reset the right_draw_tiles pointer we used for filling
    //the collision map
    right_draw_tiles = left_draw_tiles
    //now move the pointers to their correct positions
    if screen_num != 0 {
        //metatiles
        draw_counter = 0
        draw_column_with_counter_by_ptr(15,1,left_draw_tiles)
        draw_column_with_counter_by_ptr(15,1,left_draw_tiles)
        draw_counter = 0

        //attrs
        load_attr_column_by_ptr(7,1,left_draw_attrs)
    }
    
    decrement_attr_ptr(left_draw_attrs) //put the left attr pointer just off the left of the screen
    
    if screen_num != max_screen {
        //get right_draw_tiles to the right end of the screen
        //(16 columns forward)
        for i,0,until,$10 {
            //metatiles
            draw_counter = 0
            draw_column_with_counter_by_ptr(i,0,right_draw_tiles)
            draw_column_with_counter_by_ptr(i,0,right_draw_tiles)
            draw_counter = 0
            increment_draw_ptr(right_draw_tiles)
            
            //attrs
            if i & %00000001 == 0 {
                load_attr_column_by_ptr(i>>1,0,right_draw_attrs)
                increment_attr_ptr(right_draw_attrs)
            }
        }
        //metatiles
        draw_counter = 0
        draw_column_with_counter_by_ptr(0,1,right_draw_tiles)
        draw_column_with_counter_by_ptr(0,1,right_draw_tiles)
        draw_counter = 0
        
        //attrs
        load_attr_column_by_ptr(0,1,right_draw_attrs)
    }
    else {
        //get right_draw_tiles to the right end of the screen
        //(15 columns forward)
        for i,0,until,$0F {
            //metatiles
            draw_counter = 0
            draw_column_with_counter_by_ptr(i,0,right_draw_tiles)
            draw_column_with_counter_by_ptr(i,0,right_draw_tiles)
            draw_counter = 0
            increment_draw_ptr(right_draw_tiles)
            
            //attrs
            if i & %00000001 == 1 {
                load_attr_column_by_ptr(i>>1,0,right_draw_attrs)
                increment_attr_ptr(right_draw_attrs)
            }
        }
        //metatiles
        draw_counter = 0
        draw_column_with_counter_by_ptr(i,0,right_draw_tiles)
        draw_column_with_counter_by_ptr(i,0,right_draw_tiles)
        draw_counter = 0
        increment_draw_ptr(right_draw_tiles)
        
        //attrs
        load_attr_column_by_ptr(7,0,right_draw_attrs)
    }
}

void draw_status_bar() {
    //draw the status bar accross both nametables
    byte i
    
    //set ppu address increment to 32 so we can draw the left and right borders
    //(allows us to draw to the nametable in vertical strips rather than horizontal)
    ppu_ctrl = %00000000
    read_ppu_status()
    
    ppu_set_addr(ppu_nametable0+$42)
    ppu_write_data($60)
    ppu_set_addr(ppu_nametable0+$43)
    ppu_write_data($61)
    ppu_set_addr(ppu_nametable0+$62)
    ppu_write_data($62)
    ppu_set_addr(ppu_nametable0+$63)
    ppu_write_data($63)
    ppu_set_addr(ppu_nametable0+$65)
    ppu_write_data($65)
    ppu_write_data($65)
    ppu_write_data($65)
}

void draw_health_p1() {
    ppu_ctrl = %00000000
    read_ppu_status()
    ppu_set_addr(ppu_nametable0+$65)
    
    if player1.health > 0 {
        ppu_write_data($65)
    }
    else {
        ppu_write_data($64)
    }
    
    if player1.health > 1 {
        ppu_write_data($65)
    }
    else {
        ppu_write_data($64)
    }
    
    if player1.health > 2 {
        ppu_write_data($65)
    }
    else {
        ppu_write_data($64)
    }
}

void clear_status_bar() {
    //clear the status bar to all black tiles
    byte i
    
    //set ppu address increment to 1 so we can draw the left and right borders
    //(allows us to draw to the nametable in vertical strips rather than horizontal)
    ppu_ctrl = %00000000
    read_ppu_status()
    
    ppu_set_addr(ppu_nametable0)
    for i,0,until,$80 {
        ppu_write_data($24) //$24 = sky tile
    }
    
    ppu_set_addr(ppu_nametable1)
    for i,0,until,$80 {
        ppu_write_data($24) //$24 = sky tile
    }
    
    //attributes
    ppu_set_addr(ppu_attrtable0)
    for i,0,until,$20 {
        ppu_write_data($ff) //all tiles use last palette
    }
    ppu_set_addr(ppu_attrtable1)
    for i,0,until,$20 {
        ppu_write_data($ff) //all tiles use last palette
    }
    //health
    ppu_set_addr(ppu_attrtable0+1)
    ppu_write_data(%10101010)
    ppu_set_addr(ppu_attrtable1+1)
    ppu_write_data(%10101010)
}

void load_palletes(byte pallete_num) {
    byte i
    read_ppu_status() // read PPU status to reset the high/low latch
    ppu_set_addr(ppu_pallete_ram) // point the PPU to palette ram
    if pallete_num == 0 {
        for i,0,until,$20 {
            ppu_write_data(pallete0[i])
        }
    }
}

const array pallete0 = [
  $21,$15,$03,$27,$21,$1B,$0C,$29,$21,$15,$03,$27,$21,$29,$0F,$36, //nametable palettes
  $21,$09,$29,$36,$21,$06,$26,$30,$21,$01,$2C,$3C,$21,$02,$0F,$18  //sprite palettes, first byte is the background color
]