struct Sprite { //NES hardware sprite layout
    byte y,         // Y Coordinate - 1
    byte tile,      // tile index #
    byte attrs,     // attributes
    byte x,         // X Coordinate
    bool even       // arbitrary value given to sprites
                    // by whatever object is using them to ensure proper sprite flickering.
                    // The value of this should be alternated 
                    // between the sprites of a metasprite (e.g. sprite0=even,sprite1=odd, etc...)
                    // to ensure sprites flicker properly.
}



array oam_buffer [256] @$200 // sprite buffer

byte right_stack //stack variable for the left side of oam_buffer
byte left_stack //stack variable for the right side of oam_buffer
bool even_on_left_stack // Are the even sprites on the queue going on the left or right stack this frame?
                     // this value should be alternated every frame to ensure proper sprite
                     // flickering
byte sprite_queue_num   //number of sprites currently in the queue

array(pointer.Sprite) sprite_queue [63] //63 and not 64 because we never use sprite 0 for anything
                                        //other than screen splitting for the status bar

//constant offsets for oam_buffer
//so we can draw the player metasprite easily,
//in a real game you should use a oam manager
const byte playersprite0_x = 4
const byte playersprite0_tile = 5
const byte playersprite0_attrs = 6
const byte playersprite0_y = 7

const byte playersprite1_x = 8
const byte playersprite1_tile = 9
const byte playersprite1_attrs = 10
const byte playersprite1_y = 11

const byte playersprite2_x = 12
const byte playersprite2_tile = 13
const byte playersprite2_attrs = 14
const byte playersprite2_y = 15

const byte playersprite3_x = 16
const byte playersprite3_tile = 17
const byte playersprite3_attrs = 18
const byte playersprite3_y = 19

//player sprite constants
const byte sprite0_attrs = 0
const byte sprite1_attrs = 0
const byte sprite2_attrs = 0
const byte sprite3_attrs = 0
const byte sprite0_tile = 0
const byte sprite1_tile = 1
const byte sprite2_tile = 2
const byte sprite3_tile = 3

void init_sprite_manager() {
    left_stack = $00
    right_stack = $FF
    sprite_queue_num = 0
    even_on_left_stack = true
}

void draw_sprites() {
    //Take all the sprites in the queue, and move them
    //to the proper spots in the OAM buffer.
    //After that, prepare for the next time we draw sprites
    //by clearing the sprite queue and alternating the
    //even_on_left_stack variable to ensure sprite flickering
    
    //get the old sprites off the screen
    clear_stacks()
    //write the new sprites
    transfer_queue_to_oam_buffer()
    clear_sprite_queue()
    even_on_left_stack = not(even_on_left_stack)
}

void clear_sprite_queue() {
    sprite_queue_num = 0
}

void add_sprite_to_queue(pointer addr) {
    sprite_queue[sprite_queue_num] = pointer.Sprite(addr)
    sprite_queue_num += 1
}

void transfer_queue_to_oam_buffer() {
    byte i
    pointer.Sprite spr
    
    for i,0,until,sprite_queue_num {
        spr = sprite_queue[i]
        if even_on_left_stack {
            if spr->even {
                add_to_left_stack(spr)
            }
            else {
                add_to_right_stack(spr)
            }
        }
        else {
            if spr->even {
                add_to_right_stack(spr)
            }
            else {
                add_to_left_stack(spr)
            }
        }
    }
}

inline void add_to_left_stack(pointer.Sprite spr) {
    oam_buffer[left_stack] = spr->y
    oam_buffer[left_stack+1] = spr->tile
    oam_buffer[left_stack+2] = spr->attrs
    oam_buffer[left_stack+3] = spr->x
    
    left_stack += 4
}

inline void add_to_right_stack(pointer.Sprite spr) {
    oam_buffer[right_stack-3] = spr->y
    oam_buffer[right_stack-2] = spr->tile
    oam_buffer[right_stack-1] = spr->attrs
    oam_buffer[right_stack] = spr->x
    
    left_stack -= 4
}

void clear_stacks() {
    clear_left_stack()
    clear_right_stack()
}

inline void clear_left_stack() {
    while left_stack > $00 {
        left_stack -= 4 //sprites take up 4 bytes in hardware
        oam_buffer[left_stack] = $ef //move the sprite offscreen
    }
}
inline void clear_right_stack() {
    while right_stack < $FF {
        right_stack += 4 //sprites take up 4 bytes in hardware
        oam_buffer[right_stack-3] = $ef //move the sprite offscreen
    }
}

void init_sprites() {
    byte i
    for i,0,to,255 {
        if (i & %00000011) == 0 {
            //each sprite takes up 4 bytes, and we want to edit
            //the y position of each sprite (0th byte)
            //so we use the %00000011 mask to write every 4th byte (every 0th sprite byte)
            
            oam_buffer[i] = $ef // move the sprite off screen
        }
        else {
            oam_buffer[i] = 0
        }
    }
}

void prepare_sprite0() {
    //move sprite0 to overlap with bottom edge
    //of the status bar in preparation for splitting
    //the screen
    oam_buffer[0] = $16
    oam_buffer[1] = $FF
    oam_buffer[2] = $00
    oam_buffer[3] = $08
}

void update_player_sprites() {
    oam_buffer[playersprite0_x] = player1.pos.y
    oam_buffer[playersprite0_tile] = sprite0_tile
    oam_buffer[playersprite0_attrs] = sprite0_attrs
    oam_buffer[playersprite0_y] = player1.pos.x
    
    oam_buffer[playersprite1_x] = player1.pos.y
    oam_buffer[playersprite1_tile] = sprite1_tile
    oam_buffer[playersprite1_attrs] = sprite1_attrs
    oam_buffer[playersprite1_y] = player1.pos.x + 8
    
    oam_buffer[playersprite2_x] = player1.pos.y + 8
    oam_buffer[playersprite2_tile] = sprite2_tile
    oam_buffer[playersprite2_attrs] = sprite2_attrs
    oam_buffer[playersprite2_y] = player1.pos.x
    
    oam_buffer[playersprite3_x] = player1.pos.y + 8
    oam_buffer[playersprite3_tile] = sprite3_tile
    oam_buffer[playersprite3_attrs] = sprite3_attrs
    oam_buffer[playersprite3_y] = player1.pos.x + 8
}

void update_player_sprites_2() {
    oam_buffer[playersprite0_x] = player1.pos.y
    oam_buffer[playersprite0_tile] = $06
    oam_buffer[playersprite0_attrs] = sprite0_attrs
    oam_buffer[playersprite0_y] = player1.pos.x
    
    oam_buffer[playersprite1_x] = player1.pos.y
    oam_buffer[playersprite1_tile] = sprite1_tile
    oam_buffer[playersprite1_attrs] = sprite1_attrs
    oam_buffer[playersprite1_y] = player1.pos.x + 8
    
    oam_buffer[playersprite2_x] = player1.pos.y + 8
    oam_buffer[playersprite2_tile] = sprite2_tile
    oam_buffer[playersprite2_attrs] = sprite2_attrs
    oam_buffer[playersprite2_y] = player1.pos.x
    
    oam_buffer[playersprite3_x] = player1.pos.y + 8
    oam_buffer[playersprite3_tile] = sprite3_tile
    oam_buffer[playersprite3_attrs] = sprite3_attrs
    oam_buffer[playersprite3_y] = player1.pos.x + 8
}